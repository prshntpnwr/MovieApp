apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.example.movieapp"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        dataBinding.enabled = true

        androidExtensions {
            experimental = true
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField 'String', "ApiKey", api_key
            resValue 'string', "api_key", api_key
            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            buildConfigField 'String', "ApiKey", api_key
            resValue 'string', "api_key", api_key
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.room.runtime

    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.java8

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson

    implementation deps.kotlin.stdlib

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support

    implementation deps.material.material
    implementation deps.support.app_compat
    implementation deps.support.recyclerview
    implementation deps.support.cardview
    implementation deps.support.design
    implementation deps.support.v4
    implementation deps.support.grid_layout
    implementation deps.support.constraint_layout
    implementation deps.support.multidex
    implementation deps.support.annotations

    implementation deps.coroutines.core
    implementation deps.coroutines.android

    implementation deps.gson
    implementation deps.glide
    implementation deps.navigation.fragment_ktx

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    androidTestImplementation deps.navigation.testing_ktx
    kapt deps.dagger.android_support_compiler
    kapt deps.dagger.compiler
    kapt(deps.room.compiler, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    kapt deps.lifecycle.compiler
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'
    androidTestImplementation(deps.espresso.core, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation(deps.espresso.contrib, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation deps.support.test_runner
    androidTestImplementation deps.espresso.intents
    testImplementation deps.mockito.core
    testImplementation(deps.arch_core.testing, {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.mockito.core, { exclude group: 'net.bytebuddy' }
    androidTestImplementation deps.mockito.core, { exclude group: 'net.bytebuddy' }

    task fullCoverageReport(type: JacocoReport) {
        dependsOn 'createDebugCoverageReport'
        dependsOn 'testDebugUnitTest'
        reports {
            xml.enabled = true
            html.enabled = true
        }

        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                          '**/*Test*.*', 'android/**/*.*',
                          '**/*_MembersInjector.class',
                          '**/Dagger*Component.class',
                          '**/Dagger*Component$Builder.class',
                          '**/*_*Factory.class',
                          '**/*ComponentImpl.class',
                          '**/*SubComponentBuilder.class']
        def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
        def mainSrc = "${project.projectDir}/src/main/java"

        sourceDirectories = files([mainSrc])
        classDirectories = files([debugTree])
        executionData = fileTree(dir: "$buildDir", includes: [
                "jacoco/testDebugUnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ])
    }
}
